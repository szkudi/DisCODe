@ECHO OFF

if "%1"=="" GOTO NOARG

SET upperoutput=%1
SET output=%upperoutput%
CALL :UpCase upperoutput

IF EXIST %output% GOTO ALREADYEX

mkdir %output%

sed -e"s/Example/%output%/g" -e"s/EXAMPLE/%upperoutput%/g" Example/Example.hpp > %output%/%output%.hpp
sed -e"s/Example/%output%/g" -e"s/EXAMPLE/%upperoutput%/g" Example/Example.cpp > %output%/%output%.cpp
sed -e"s/Example/%output%/g" -e"s/EXAMPLE/%upperoutput%/g" Example/CMakeLists.txt > %output%/CMakeLists.txt

GOTO:EOF



:NOARG
echo Usage: create.bat COMPONENT_NAME
GOTO:EOF



:ALREADYEX
echo Directory %output% already exists. Choose different name.
GOTO:EOF



:LoCase
:: Subroutine to convert a variable VALUE to all lower case.
:: The argument for this subroutine is the variable NAME.
FOR %%i IN ("A=a" "B=b" "C=c" "D=d" "E=e" "F=f" "G=g" "H=h" "I=i" "J=j" "K=k" "L=l" "M=m" "N=n" "O=o" "P=p" "Q=q" "R=r" "S=s" "T=t" "U=u" "V=v" "W=w" "X=x" "Y=y" "Z=z") DO CALL SET "%1=%%%1:%%~i%%"
GOTO:EOF



:UpCase
:: Subroutine to convert a variable VALUE to all UPPER CASE.
:: The argument for this subroutine is the variable NAME.
FOR %%i IN ("a=A" "b=B" "c=C" "d=D" "e=E" "f=F" "g=G" "h=H" "i=I" "j=J" "k=K" "l=L" "m=M" "n=N" "o=O" "p=P" "q=Q" "r=R" "s=S" "t=T" "u=U" "v=V" "w=W" "x=X" "y=Y" "z=Z") DO CALL SET "%1=%%%1:%%~i%%"
GOTO:EOF



:TCase
:: Subroutine to convert a variable VALUE to Title Case.
:: The argument for this subroutine is the variable NAME.
FOR %%i IN (" a= A" " b= B" " c= C" " d= D" " e= E" " f= F" " g= G" " h= H" " i= I" " j= J" " k= K" " l= L" " m= M" " n= N" " o= O" " p= P" " q= Q" " r= R" " s= S" " t= T" " u= U" " v= V" " w= W" " x= X" " y= Y" " z= Z") DO CALL SET "%1=%%%1:%%~i%%"
GOTO:EOF